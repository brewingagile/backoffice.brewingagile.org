buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.1.0"
    }
}

plugins {
    id "com.palantir.git-version" version "0.12.2"
}

subprojects {
    repositories {
        mavenCentral()
    }
}

project(':application') {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    sourceCompatibility = 12
    targetCompatibility = 12


    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            resources {
                srcDir 'src/main/resources'
            }
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.18.8"
        annotationProcessor "org.projectlombok:lombok:1.18.8"

        implementation 'org.postgresql:postgresql:42.2.1'
        implementation 'net.sourceforge.argo:argo:5.5'

        implementation 'com.hencjo.summer:summer-security:1.1.1'
        implementation 'com.hencjo.summer:summer-migration:1.0'

        implementation 'org.eclipse.jetty:jetty-server:9.4.17.v20190418'
        implementation 'org.eclipse.jetty:jetty-servlet:9.4.17.v20190418'
        implementation 'org.eclipse.jetty:jetty-webapp:9.4.17.v20190418'

        implementation 'org.functionaljava:functionaljava:4.5'
        implementation 'org.functionaljava:functionaljava-java8:4.5'

        implementation 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.7'
        implementation 'org.glassfish.jersey.media:jersey-media-multipart:2.7'
        implementation 'org.glassfish.jersey.core:jersey-client:2.7'

        implementation 'org.apache.commons:commons-email:1.3.1'
        implementation 'args4j:args4j:2.0.16'

        implementation 'com.squareup.okhttp3:okhttp:3.6.0'
    }

    jar {
        manifest {
            attributes("Main-Class": "org.brewingagile.backoffice.application.Main")
            attributes("Implementation-Version": gitVersion())
        }
    }

    build.dependsOn(shadowJar)
}

wrapper {
    gradleVersion = '5.6.1'
}